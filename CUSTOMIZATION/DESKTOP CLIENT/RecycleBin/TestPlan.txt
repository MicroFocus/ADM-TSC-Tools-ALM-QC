'/////////Code Added By saas_tphan/////////////////////////////////////////////
'Implement code for Recycle Bin in Test Plan
Function Test_CanDelete(Entity)
  On Error Resume Next
 'RecycleBin feature for tests
  If User.IsInGroup("TDAdmin") Then
     Test_CanDelete = True
  Else
     Test_CanDelete = False

     if not inRecycleBin(Test_Fields.Field("TS_SUBJECT").Value.Path ) then
        Msgbox "Moving selected test to Recycle Bin."
        Entity.Field("TS_SUBJECT") = CLng(RecycleBin_TestPlan)
        Entity.Post
        Actions.Action("act_filter_refresh").Execute
     else
      msgbox "You do not have the required permission to permanently delete this test."
     end if
  End If

  On Error GoTo 0
End Function

Function TestFolder_CanDelete(Entity)
On Error Resume Next

  If User.IsInGroup("TDAdmin") Then
     if Entity.Path =  "Subject\_Recycle Bin" then
         TestFolder_CanDelete = false
     else
         TestFolder_CanDelete = True
     end if

  Else
    TestFolder_CanDelete = False
    if not inRecycleBin(Entity.Path) then
      Msgbox "Moving selected folder to Recycle Bin."
      call MoveTestPlanFolder(Entity.Path, "Subject\_Recycle Bin")
      Actions.Action("act_filter_refresh").Execute
    else
      msgbox "You do not have the required permission to permanently delete this folder."
    end if
  end if


On Error GoTo 0
End Function

'Code to move folder to Recycle Bin
Sub MoveTestPlanFolder(sourceFolderPath,destFolderPath)
On Error Resume Next

     Dim treeMng 'As TreeManager
     Dim sourceFolder 'As SubjectNode
     Dim destFolder 'As SubjectNode
     set tdc = TDConnection
     Set treeMng = tdc.TreeManager
     Set sourceFolder = treeMng.NodeByPath(sourceFolderPath)
     Set destFolder = treeMng.NodeByPath(destFolderPath)
     sourceFolder.move(destFolder.NodeID)
     if err.number <> 0 then
        msgbox err.description  & ". Please rename the folder before deleting."
     end if
     destFolder.Refresh


On Error GoTo 0
End Sub

'Check to see if a folder is under the Recycle Bin
Function inRecycleBin(sourceFolderPath)
On Error Resume Next

    If instr(sourceFolderPath,"Subject\_Recycle Bin") > 0 then
       inRecycleBin = true
    else
       inRecycleBin = False
    end if

On Error GoTo 0
End Function

'Prevent user from rename the Recycle Bin folder
Function TestFolder_FieldCanChange(FieldName, NewValue)
  On Error Resume Next
     'Prevent Folder from being renamed
     If FieldName = "AL_DESCRIPTION"  then
       If clng(TestFolder_Fields.Field("AL_ITEM_ID").Value) = clng(RecycleBin_TestPlan) then
         TestFolder_FieldCanChange = false
         exit function
       End if
     End if
  TestFolder_FieldCanChange = DefaultRes
  On Error GoTo 0
End Function