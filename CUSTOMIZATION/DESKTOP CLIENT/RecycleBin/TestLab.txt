'//////////////////////////////////////////////////////////////////////////////
'Added by sass_tphan for recycle bin
'//////////////////////////////////////////////////////////////////////////////
Function TestSetFolder_CanDelete(Entity)
  On Error Resume Next

  If User.IsInGroup("TDAdmin") Then
     if  Entity.Path = "Root\_Recycle Bin"  then
       TestSetFolder_CanDelete = false
     else
       TestSetFolder_CanDelete = true
     end if
  Else
    TestSetFolder_CanDelete = False
    if not inRecycleBinTL(Entity.Path) then
      Msgbox "Moving selected test set folder to Recycle Bin."
      call MoveTestSetFolder(Entity.Path, "Root\_Recycle Bin")
      Actions.Action("actFilterRefresh").Execute
    else
      msgbox "You do not have the required permission to permanently delete this folder."
    end if
  end if


  On Error GoTo 0
End Function

'Code to move folder to recycle bin
Sub MoveTestSetFolder(sourceFolderPath,destFolderPath)
On Error Resume Next

     Dim treeMng 'As TestSetTreeManager
     Dim sourceFolder 'As TestSetFolder
     Dim destFolder 'As TestSetFolder
     set tdc = TDConnection
     Set treeMng = tdc.TestSetTreeManager
     Set sourceFolder = treeMng.NodeByPath(sourceFolderPath)
     Set destFolder = treeMng.NodeByPath(destFolderPath)
     sourceFolder.move(destFolder.NodeID)
     if err.number <> 0 then
        msgbox err.description  & ". Please rename the folder before deleting."
     end if
     destFolder.Refresh

On Error GoTo 0
End Sub

'Check to see if a folder is under the recycle bin
Function inRecycleBinTL(sourceFolderPath)
On Error Resume Next

    If instr(sourceFolderPath,"Root\_Recycle Bin") > 0 then
       inRecycleBinTL = true
    else
       inRecycleBinTL = False
    end if

On Error GoTo 0
End Function

Function TestSet_CanDelete(Entity)
  On Error Resume Next
  'RecycleBin feature for tests
  If User.IsInGroup("TDAdmin") Then
     TestSet_CanDelete = True
  Else
     TestSet_CanDelete = False

     if not inRecycleBinTL(TestSet_Fields.Field("CY_CYCLE").Value.Path ) then
        Msgbox "Moving selected test set to Recycle Bin."
        Entity.Field("CY_FOLDER_ID") = CLng(RecycleBin_TestLab)
        Entity.Post
        Actions.Action("actFilterRefresh").Execute
     else
      msgbox "You do not have the required permission to permanently delete this test set."
     end if
  End If
  On Error GoTo 0
End Function

'Set the Deleted column (RN_USER_01) TO "Y" when user select to delete the run
Function Run_CanDelete(Entity)
  On Error Resume Next

     If User.IsInGroup("TDAdmin") Then
        Run_CanDelete = True
     Else
        Run_CanDelete = false
        Entity.Field("RN_USER_01") = "Y"
        Entity.Post
        Actions.Action("actFilterRefresh").Execute
        msgbox "You do not have the required permission to permanently delete this run."
     End if
  On Error GoTo 0
End Function

'Set the Deleted column (TC_USER_01) TO "Y" when user select to delete the test in test set
Function TestSetTests_CanDelete(Entity)
  On Error Resume Next
     If User.IsInGroup("TDAdmin") Then
        TestSetTests_CanDelete = True
     Else
        TestSetTests_CanDelete = false
        Entity.Field("TC_USER_01") = "Y"
        Entity.Post
        Actions.Action("actFilterRefresh").Execute
        msgbox "You do not have the required permission to permanently delete this test in testset."
     End if
  On Error GoTo 0
End Function

'Prevent user from renaming Recycle Bin folder
Function TestSetFolder_FieldCanChange(FieldName, NewValue)
  On Error Resume Next
     'Prevent Folder from being renamed
     If FieldName = "CF_ITEM_NAME" then
       If clng(TestSetFolder_Fields("CF_ITEM_ID").Value) = clng(RecycleBin_TestLab) then
         TestSetFolder_FieldCanChange = false
         exit function
       End if
     End if
  TestSetFolder_FieldCanChange = DefaultRes
  On Error GoTo 0
End Function