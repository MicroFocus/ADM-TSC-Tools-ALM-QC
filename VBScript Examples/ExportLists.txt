'Script to download the list into excel using Quality Center Open Test Architecture(QC OTA)
' arg 0: QC URL
' arg 1: QC user
' arg 2: QC password
' arg 3: QC domain
' arg 4: QC project
' arg 5: path
 
' call it like
'c:/windows/syswow64\cscript ExportLists.vbs https://<almhost>/qcbin/ <user> <password> <domain> <project> <path>


Public index

'////////////////////////////////////////////////////////////////////////////////////
'//
'// ---'Output to console---
'///////////////////////////////////////////////////////////////////////////////////
Public Sub Output (Content)
	WScript.StdOut.WriteLine Content
End Sub

'////////////////////////////////////////////////////////////////////////////////////
'//
'// ---'Export data to Excel
'///////////////////////////////////////////////////////////////////////////////////
Sub Export(tdConnection, savepath)
On Error resume next
    Dim Excel, Sheet

    output "Exporting list information to Excel."
    Set Excel = CreateObject("Excel.Application") 'Open Excel
    Excel.WorkBooks.Add() 'Add a new workbook
    'Get the first worksheet.
    Set Sheet = Excel.ActiveSheet
   
    'sheet name as Lists
    Sheet.Name = "Lists"
   
   'Specify the Excel Sheet Properties  
    With Sheet.Range("A1:H1")
        .Font.Name = "Arial"
        .Font.FontStyle = "Bold"
        .Font.Size = 10
        .Font.Bold = True
        .Interior.ColorIndex = 15 'Light Grey
   End With
   
   'Excel Sheet Column Header Values
   
   'Subject fields
    Sheet.Cells(1, 1) = "List Name"
    Sheet.Cells(1, 2) = "List Path"
    
    Dim TreeMgr
   	Dim cust
	Dim i
	
	'output "Get Customization object."
    'Connection to TreeManage
	set cust = tdConnection.Customization
	Set custlists = cust.Lists

	index = 2
	For i = 1 To custlists.Count
			
			Set mylist = custlists.ListByCount(i)
			'output "List:" &  mylist.Name
			Sheet.Cells(index, 1) = mylist.Name
			index = index + 1
			Set objRoot  = mylist.RootNode
			Call TraverseTree(objRoot, Sheet,mylist.Name)
	Next

   
    Excel.Columns.AutoFit

    'Save the newly created workbook and close Excel.
    Excel.ActiveWorkbook.SaveAs(savepath &  "\List_ " & TimeStamp & ".xlsx")
    Excel.Quit
    
	output "Data saved to " & savepath &  "\List_ " & TimeStamp & ".xlsx"
   
    Set Excel = Nothing
	set custlist = nothing
	set cust = nothing
	set objRoot = nothing
    Set TreeMgr = Nothing
	
On error goto 0
End Sub

Public Sub TraverseTree(objNode,Sheet,currPath )
on error resume next  
   Dim nodepath
   nodepath = ""
   if objNode.childrencount = 0 then
		'No children
   else
	for each subnode in objNode.children
		nodepath = currPath & "\" & subnode.name
		Sheet.Cells(index, 2) = nodepath
		index = index + 1

		call TraverseTree(subnode,Sheet,nodepath) 
	next
   end if
   
 
On error goto 0        
End Sub
'////////////////////////////////////////////////////////////////////////////////////
'//
'// ---'Timestamp for log
'// 
'///////////////////////////////////////////////////////////////////////////////////
Function LZ(ByVal Number)
  If Number < 10 Then
    LZ = "0" & CStr(Number)
  Else
    LZ = CStr(Number)
  End If
End Function

Function TimeStamp
  Dim CurrTime
  CurrTime = Now()

  TimeStamp = CStr(Year(CurrTime)) & "-" _
    & LZ(Month(CurrTime)) & "-" _
    & LZ(Day(CurrTime)) & " " _
    & LZ(Hour(CurrTime)) & "_" _
    & LZ(Minute(CurrTime)) & "_" _
    & LZ(Second(CurrTime))
End Function
 
 
'////////////////////////////////////////////////////////////////////////////////////
'//
'// ---'Main---
'// ---'Connect to the Quality Center Server.


'////////////////////////////////////////////////////////////////////////////////////
'//
'// --- Main ---
'///////////////////////////////////////////////////////////////////////////////////
Sub Main
On Error Resume next
Dim args
Dim tdConnection
Dim QcURL
Dim QcUser
Dim QcPassword
Dim QcDomain
Dim QcProject
Dim QcSavePath

Set args = WScript.Arguments
QcURL = args.Item(0)
QcUser = args.Item(1)
QcPassword = args.Item(2)
QcDomain = args.Item(3)
QcProject = args.Item(4)
QcSavePath = args.Item(5)

output "ALM Server:" & QcURL
output "Domain:" & QCDomain
output "Projetc:" & QcProject

Set tdConnection = CreateObject("TDApiOle80.TDConnection.1")
If tdConnection Is Nothing Then
	Output err.description
Else
	Output "Connecting to ALM server..."
	tdConnection.InitConnectionEx QcURL
	If tdConnection.Connected = False Then
		Output err.description
	Else
		Output "Authenticating..."
		tdConnection.Login QCUser, QcPassword
		If tdConnection.LoggedIn = False Then
			Output err.description
		Else
			Output "Connecting to project " & QcDomain & "." & QcProject
			tdConnection.Connect QCDomain, QCProject

			If tdConnection.ProjectConnected = False Then
				Output  err.descrption
			Else
				
				Call Export(tdConnection,QcSavePath)			    
				tdConnection.DisconnectProject
			End If
			tdConnection.Logout
		End If
		tdConnection.ReleaseConnection
		output "QC Disconnected."
	End If
End If	

set tdConnection = nothing

End Sub

'////////////////////////////////////////////////////////////////////////////////////
'//
'// --- Call Main ---
'///////////////////////////////////////////////////////////////////////////////////
call Main