' DownloadBPTComponent.vbs
' Download BPT scripts of a given QC project
' arg 0: QC URL
' arg 1: QC user
' arg 2: QC password
' arg 3: QC domain
' arg 4: QC project
' arg 5: path
' arg 6: test id

 
' call it like
' cscript DownloadBPTComponent.vbs https://<almhost>/qcbin/ <user> <password> <domain> <project> <path>
'Example:
'c:\windows\syswow64\cscript DownloadBPTComponent.vbs https://serverurl/qcbin user password domain project c:\temp


'////////////////////////////////////////////////////////////////////////////////////
'//
'// ---'Output to console---
'///////////////////////////////////////////////////////////////////////////////////
Public Sub Output (Content)
	WScript.StdOut.WriteLine Content
End Sub

'////////////////////////////////////////////////////////////////////////////////////
'//
'// ---'Download BPT component ---
'///////////////////////////////////////////////////////////////////////////////////
Sub getBPTComponent(TDConnection, path)
On error resume next
	Dim compFoldFact
	Dim compFoldList
	Dim compFolder
	Dim compFact
	Dim compList
	Dim comp
	Dim compStorage
	Dim CompDownLoadPath

	'Get list of component folder
	Set compFoldFact = tdConnection.ComponentFolderFactory 
	set compFoldList = compFoldFact.newlist("")
	
	for each compFolder in compFoldList
	   'Get the component factory for each folder
	   set compFact = compFolder.ComponentFactory
	   set compList = compFact.newlist("")
	   for each comp in compList
	   	
	   	output "downloading component id:" & comp.id
	   	err.clear
	   	Set compStorage = comp.ExtendedStorage(0)
		if err.number <> 0 then
			output err.descrption
		end if
		
		'Need to insure that the path folder exist
		compStorage.ClientPath = path & "\" & comp.Name 
		
		'Use IExtendedStorage.Load to get the component files.
		output "Downloading " & comp.name & " to " & path & "\" & comp.Name 
		err.clear
		CompDownLoadPath = compStorage.Load("-r *.*", True)
		if err.number <> 0 then
			output err.descrption
		end if
		
	   next
	next
	
	set compFoldFact = nothing
	set compFoldList = nothing
	set compFolder = nothing
	set compFact = nothing
	set compList = nothing
	set comp = nothing
	set compStorage = nothing
	
End Sub


'////////////////////////////////////////////////////////////////////////////////////
'//
'// --- Main ---
'///////////////////////////////////////////////////////////////////////////////////
Sub Main
On Error Resume next
Dim args
Dim tdConnection
Dim QcURL
Dim QcUser
Dim QcPassword
Dim QcDomain
Dim QcProject



Set args = WScript.Arguments
QcURL = args.Item(0)
QcUser = args.Item(1)
QcPassword = args.Item(2)
QcDomain = args.Item(3)
QcProject = args.Item(4)
path = args.Item(5)


Set tdConnection = CreateObject("TDApiOle80.TDConnection.1")
If tdConnection Is Nothing Then
	Output "Could not create TD Connection object"
Else
	tdConnection.InitConnectionEx QcURL
	AttachmentNameList = ""
	If tdConnection.Connected = False Then
		Output "Could not initialize QC connection"
	Else
		tdConnection.Login QCUser, QcPassword
		If tdConnection.LoggedIn = False Then
			Output "Could not log into QC"
		Else
			Output "Connecting to project " & QcDomain & "." & QcProject
			tdConnection.Connect QCDomain, QCProject

			If tdConnection.ProjectConnected = False Then
				Output "Invalid domain/project"
			Else
				
				getBPTComponent tdconnection,path			    
				tdConnection.DisconnectProject
			End If
			tdConnection.Logout
		End If
		tdConnection.ReleaseConnection
		output "QC Disconnected."
	End If
End If	

set tdConnection = nothing

End Sub

'////////////////////////////////////////////////////////////////////////////////////
'//
'// --- Call Main ---
'///////////////////////////////////////////////////////////////////////////////////
call Main

